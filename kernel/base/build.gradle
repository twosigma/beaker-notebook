/*
 *  Copyright 2017 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'de.undercouch:gradle-download-task:3.3.0'
  }
}

apply plugin: 'de.undercouch.download'
apply plugin: 'java'
apply plugin: 'maven-publish'

ext.kernelIdName = 'base'

repositories {
  mavenCentral()
}

compileJava {
  options.compilerArgs << '-Xlint:sunapi'
  options.compilerArgs << '-XDenableSunApiLintControl'
}

dependencies {
  compile group: 'com.opencsv', name: 'opencsv', version: '3.10'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.5'
  compile group: 'com.github.jupyter', name: 'jvm-repr', version: '0.2.1'
  compile group: 'com.google.guava', name: 'guava', version: '22.0'
  compile group: 'com.google.inject', name: 'guice', version: '3.0'
  compile group: "commons-codec", name: "commons-codec", version: "1.9"
  compile group: 'commons-io', name: 'commons-io', version: '2.5'
  compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
  compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'
  compile group: 'org.apache.maven.shared', name: 'maven-invoker', version: '3.0.0'
  compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.22'
  compile group: "org.zeromq", name: "jeromq", version: "0.3.5"

  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
  testCompile group: 'org.reflections', name: 'reflections', version: '0.9.10'
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'com.twosigma'
      artifactId 'beaker-kernel-base'
      version '2.0-SNAPSHOT'

      from components.java
    }
  }
}

def gitDescribe() {
  def name = "git describe --all".execute().text.trim()
  if (name.startsWith('heads/')) {
    name = name.substring(6)
  }
  return name
}

def gitGetHash() {
  def name = "git log --pretty=format:%h -n 1".execute().text.trim()
  return name
}

def resourceDir = file("./src/main/resources")

task prepareBuildVerion() {
  doLast {
    new File(resourceDir, 'version').write(gitDescribe())
    new File(resourceDir, 'hash').write(gitGetHash())
    new File(resourceDir, 'build_time').write(new Date().format("yyyy-MM-dd HH:mm z"))
  }
}

compileJava.dependsOn prepareBuildVerion

task copyDependencies(type: Copy) {
  from configurations.runtime
  from jar
  into new File(pathToStaticContent, 'base/lib')
}

jar.finalizedBy copyDependencies

import de.undercouch.gradle.tasks.download.Download

task downloadMaven(type: Download) {
  src 'http://ftp.ps.pl/pub/apache/maven/maven-3/3.5.2/binaries/apache-maven-3.5.2-bin.zip'
  dest file('libs/')
}

task unzipMaven(type: Copy) {
  def zipFile = file('libs/apache-maven-3.5.2-bin.zip')
  def outputDir = file(pathToStaticContent + '/base')

  from zipTree(zipFile)
  into outputDir

  doLast {
    new File(resourceDir, '/mvnLocation.txt').write(outputDir.canonicalPath + "/apache-maven-3.5.2")
  }
}

if (!file('libs/apache-maven-3.5.2-bin.zip').exists()) {
  file('libs/').mkdir()
  compileJava.finalizedBy downloadMaven
  downloadMaven.finalizedBy unzipMaven
} else {
  compileJava.finalizedBy unzipMaven
}


staticContent {
  ext.executeStaticContent = false
}
